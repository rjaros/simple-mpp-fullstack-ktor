buildscript {
    repositories {
        mavenLocal()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://kotlin.bintray.com/kotlinx' }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${kotlinVersion}"
        classpath "com.github.jengelman.gradle.plugins:shadow:${gradleShadowPluginVersion}"
    }
}

apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

kotlin {
    targets {
        fromPreset(presets.jvm, 'backend')
        fromPreset(presets.js, 'frontend') {
            [tasks.getByName(compilations.main.compileKotlinTaskName), tasks.getByName(compilations.test.compileKotlinTaskName)]*.kotlinOptions {
                languageVersion = "1.3"
                moduleKind = "umd"
                sourceMap = true
                metaInfo = true
                outputFile = "$project.buildDir.path/classes/kotlin/frontend/main/frontend.js"
            }
        }
    }
    sourceSets {
        backendMain
        frontendMain
    }
}

repositories {
    jcenter()
    maven { url = 'https://kotlin.bintray.com/ktor' }
    maven { url = 'https://kotlin.bintray.com/kotlin/kotlinx' }
    maven { url = 'https://kotlin.bintray.com/kotlinx' }
    maven { url = 'https://dl.bintray.com/gbaldeck/kotlin' }
    maven { url = 'https://dl.bintray.com/rjaros/kotlin' }
    mavenLocal()
}

dependencies {
    backendMainImplementation "io.ktor:ktor-server-netty:$ktorVersion"
    backendMainImplementation "io.ktor:ktor-html-builder:$ktorVersion"
    backendMainImplementation "ch.qos.logback:logback-classic:$logbackVersion"
    backendMainImplementation "pl.treksoft:kvision-server-ktor:${kvisionVersion}"

    commonMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-common"
    commonMainImplementation "pl.treksoft:kvision-common-types:${kvisionVersion}"
    commonMainImplementation "pl.treksoft:kvision-common-remote:${kvisionVersion}"
    commonTestImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
    commonTestImplementation "org.jetbrains.kotlin:kotlin-test-common"

    frontendMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-js"
    frontendMainImplementation "pl.treksoft:kvision-remote:${kvisionVersion}"
    frontendTestImplementation "org.jetbrains.kotlin:kotlin-test-js"

    backendMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    backendTestImplementation "org.jetbrains.kotlin:kotlin-test"
    backendTestImplementation "org.jetbrains.kotlin:kotlin-test-junit"
}


def webFolder = new File(project.buildDir, "web")

def frontendCompilations = kotlin.targets.frontend.compilations

task populateWebFolder(dependsOn: [frontendMainClasses]) {
    doLast {
        copy {
            from frontendCompilations.main.output
            from kotlin.sourceSets.frontendMain.resources.srcDirs
            frontendCompilations.test.runtimeDependencyFiles.each {
                if (it.exists() && !it.isDirectory()) {
                    from zipTree(it.absolutePath).matching { include '*.js' }
                }
            }
            into webFolder
        }
    }
}

frontendJar.dependsOn(populateWebFolder)

task run(type: JavaExec, dependsOn: [backendMainClasses, frontendJar]) {
    main = "io.ktor.server.netty.EngineMain"
    classpath {
        [
                kotlin.targets.backend.compilations.main.output,
                configurations.backendRuntimeClasspath,
        ]
    }
    args = []
}
